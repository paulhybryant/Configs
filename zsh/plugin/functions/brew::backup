# vim: filetype=zsh sw=2 ts=2 sts=2 et tw=80 foldlevel=0 nospell

echo "#!/usr/bin/env zsh"
echo ""

for tap in $(brew tap | sort); do
  echo "brew tap $tap"
done

local brewlinfo
brewinfo="/tmp/brew.json"
brew info --json=v1 --installed >! $brewinfo

# Find out formulae installed with custom options
# e.g brew install python --with-unicode-ucs4
local -a formulae_with_options
formulae_with_options=($(cat $brewinfo | jq -r '.[] | select(.installed[0].used_options != []) | .full_name' | sort))

# Find out formulae installed with HEAD
local -a head_formulae
head_formulae=($(cat $brewinfo | jq -r '.[] | select(.installed[0].version | contains("HEAD")) | .full_name' | sort))

local jq_str
local options
for formula in $(brew leaves | grep -v 'gem-.*' | sort); do
  if [[ $formula == ${head_formulae[(r)$formula]} ]]; then
    echo "brew install --HEAD ${formula}"
  elif [[ $formula == ${formulae_with_options[(r)$formula]} ]]; then
    jq_str='.[] | select(.installed[0].used_options != [] and .full_name == "'
    jq_str+=$formula
    jq_str+='") | . as $in | reduce .installed[0].used_options[] as $item (""; . + " " + $item) | .'
    options="$(cat $brewinfo | jq -r $jq_str)"
    echo "brew install ${formula} ${options}"
  else
    echo "brew install ${formula}"
  fi
done

# Handle brew gem installed formula
for formula in $(brew leaves | grep 'gem-.*' | sed 's/gem-//'); do
  echo "brew gem install ${formula} --homebrew-ruby"
done

if [[ $OSTYPE == *darwin* ]]; then
  for cask in $(brew cask list | sort); do
    echo "brew cask install $cask"
  done
fi
